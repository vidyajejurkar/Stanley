import fr.xgouchet.gradle.AndroidBuild

plugins {
    alias libs.plugins.kotlin.android
    alias libs.plugins.kotlin.kapt
    alias libs.plugins.detekt
    alias libs.plugins.dependency.versions
}

apply plugin: 'com.android.application'
apply plugin: 'ossLicenses'

android {

    defaultConfig {
        compileSdk AndroidBuild.TargetSdk
        buildToolsVersion = AndroidBuild.BuildTools

        minSdkVersion AndroidBuild.MinSdk
        targetSdkVersion AndroidBuild.TargetSdk

        applicationId "fr.xgouchet.packageexplorer"
        versionCode 20
        versionName "2.8"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    /** The build types */
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    // Android
    implementation libs.bundles.androidx
    implementation libs.bundles.android.ui
    implementation libs.multidex

    implementation libs.bundles.kotlin
    implementation libs.bundles.rx
    implementation libs.gson
    implementation libs.aboutpage
    implementation libs.timber
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
